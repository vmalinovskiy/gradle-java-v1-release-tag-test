// Currently the "plugins" notation cannot be used for applying the plugin for sub projects, when used from the root build script.
plugins {
    id "com.jfrog.bintray" version "1.6"
}

task clientFatJar(type: Jar, dependsOn: jar) {
    classifier = 'all'
    from {
        [
            sourceSets.main.output,
            configurations.runtime.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        ]
    }
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.1'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.6.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.1'

    testCompile project(":callfire-api-1.1-client-test")
}

bintray {
    user = System.properties[ 'BINTRAY_USER' ] ?: "user not set"
    key = System.properties[ 'BINTRAY_KEY' ] ?: "key not set"

    configurations = [ 'archives' ]
    publish = true

    pkg {
        repo = 'maven'
        // userOrg = 'Callfire'
        name = project.name
        desc = 'Callfire API 1.1 java client'
        websiteUrl = 'https://developers.callfire.com'
        issueTrackerUrl = 'https://github.com/CallFire/callfire-api-1.1-client-java/issues'
        vcsUrl = 'https://github.com/CallFire/callfire-api-1.1-client-java.git'
        licenses = [ 'MIT' ]
        labels = [ 'callfire', 'api', 'client' ]
        publicDownloadNumbers = true
    }
}
