buildscript {
    repositories {
        jcenter()
    }
}

subprojects { p ->
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        jcenter()
    }

    processResources {
        expand(p.properties)
    }

    task generatePom << {
        pom {
            //noinspection GroovyAssignabilityCheck
            project {
                //noinspection GroovyAssignabilityCheck
                inceptionYear '2016'
                licenses {
                    license {
                        name "The MIT License (MIT)"
                        url "https://opensource.org/licenses/MIT"
                        distribution "repo"
                    }
                }
                developers {
                    developer {
                        id "vladimir-mhl"
                        name "Vladimir Mikhailov"
                        email "vmikhailov@callfire.com"
                    }
                }
            }
        }.writeTo("build/META-INF/maven/${p.group}/${p.name}/pom.xml")
    }

    jar {
        into('META-INF') {
            from 'build/META-INF'
        }
    }
    jar.dependsOn generatePom

    // artifacts build config
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from {
            sourceSets.main.allSource
        }
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from {
            javadoc.destinationDir
        }
    }

    tasks.withType(Jar) {
        //noinspection GroovyAssignabilityCheck
        manifest {
            //noinspection GroovyAssignabilityCheck
            attributes(
                'Manifest-Version': '1.0',
                'Implementation-Title': p.name,
                'Implementation-Version': p.version,
                'Implementation-Build-Number': buildNumber(),
                'Implementation-Vendor': 'Callfire, Inc.',
                'Implementation-Build-Date': new Date().format('yyyy-MM-dd'),
                'Documentation': 'https://developers.callfire.com',
                'Repository': 'https://github.com/CallFire/callfire-api-1.1-client-java'
            )
        }
        into('META-INF') {
            from 'LICENSE'
        }
    }

    build.dependsOn javadoc

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    dependencies {
        compile 'org.apache.httpcomponents:httpclient:4.5'
        compile 'org.apache.httpcomponents:httpmime:4.5'
        compile 'org.apache.commons:commons-lang3:3.4'

        testCompile 'org.skyscreamer:jsonassert:1.2.3'
        testCompile('junit:junit:4.12') {
            exclude module: 'hamcrest'
            exclude module: 'hamcrest-core'
        }
        testCompile("org.mockito:mockito-core:1.10.19") {
            exclude module: 'hamcrest'
            exclude module: 'hamcrest-core'
        }
        testCompile 'org.hamcrest:hamcrest-all:1.3'
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

def buildNumber() {
    String buildNumber = System.properties[ 'BUILD_NUMBER' ] ?: "SNAPSHOT"
    "$buildNumber"
}
